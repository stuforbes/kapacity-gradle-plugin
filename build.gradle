/*
 * Copyright (c) 2020 com.stuforbes
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

buildscript {

    ext {
        kotlinVersion = "1.3.61"
        kapacityCoreVersion = "0.1.6"

        junitVersion = "5.6.0"
        mockkVersion = "1.9"
        assertJVersion = "3.11.1"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'net.researchgate.release' version '2.8.1'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
}

def props = new Properties()
file("version.properties").withInputStream { props.load(it) }

group 'com.stuforbes.kapacity'
version props.getProperty('version')

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.stuforbes.kapacity:kapacity-core:${kapacityCoreVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

release {
    versionPropertyFile = 'version.properties'
}

pluginBundle {
    website = 'https://github.com/stuforbes/kapacity-gradle-plugin'
    vcsUrl = 'https://github.com/stuforbes/kapacity-gradle-plugin'
    tags = ['kapacity', 'capacity testing', 'performance testing']
}

gradlePlugin {
    plugins {
        kapacity {
            id = 'com.stuforbes.kapacity'
            displayName = 'Kapacity Gradle Plugin'
            description = 'Plugin to execute Kapacity (https://github.com/stuforbes/kapacity) test suites'
            implementationClass = 'com.stuforbes.kapacity.KapacityPlugin'
        }
    }
}